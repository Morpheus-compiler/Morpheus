working_directory = system("dirname $(readlink -f ".ARG0.")")

set terminal postscript eps color solid enhanced font ',22' size 3.7,2.5
set encoding utf8
set output working_directory."/../figures/eval-dynamic-traffic.eps"

set key top inside vertical right Right font "Helvetica,20"
set key at screen 0.92,screen 0.92
set key samplen 2.5
set grid lw 0.1

set boxwidth 0.9

set ylabel "Throughput (Mpps)" font "Helvetica,22" offset 1.5,0
set xlabel "Time (s)" font "Helvetica,22"
set xrange [1:20]

set ytics 1 nomirror
set datafile separator ","

set label "High\nlocality" font "Helvetica,18" at screen 0.435,0.52 center
set label "High locality\nwith new set\n of flows" font "Helvetica,18" at screen 0.77,0.7 center

#####################################
## Define colors used in the graph ##
#####################################
O0_COLOR = "#542437"                     #006400
O1_COLOR = "#53777a"                     #FFFF00
O2_COLOR = "#ffff00"                     #FFA500
O3_COLOR = "#ff6361"                     #FF0000
BOLT_COLOR = "#fa6900"                   #FF5145
AUTOFDO_COLOR = "#a7dbd8"                #FF9541
BOLT_AUTOFDO_COLOR = "#83af9b"           #FF4611
CACHED_COMP_COLOR = "#009900"
RUNTIME_CONF_COLOR = "#cc0000"    #ec7176"
MAT_SPECIALIZATION_COLOR = "#0000ff"       #f4ab33"     #FD1741
BLACK = "black"
OPTIMIZATION_COLOR = "#ff0000"

first_time1=system("sort -nk 1 ".working_directory."/../data/eval-dynamic-traffic/results_dynamic_traffic_baseline.csv | head -n 2 | awk 'BEGIN { FS = \",\" } ; NR==2{print $1}'")

first_time2=system("sort -nk 1 ".working_directory."/../data/eval-dynamic-traffic/results_dynamic_traffic_morpheus.csv | head -n 2 | awk 'BEGIN { FS = \",\" } ; NR==2{print $1}'")

set parametric
set trange [2:6]

const5=5
const10=10
const20=20
const35=35

plot \
	working_directory.'/../data/eval-dynamic-traffic/results_dynamic_traffic_baseline.csv'	using ($1-first_time1+1):4 with lines lw 8 lc rgb RUNTIME_CONF_COLOR ti "Baseline", \
	working_directory.'/../data/eval-dynamic-traffic/results_dynamic_traffic_morpheus.csv'	using ($1-first_time2+1):4 with lines lw 8 lc rgb CACHED_COMP_COLOR ti "Morpheus", \
	const5,t ls 3 lw 8 dt 2 lc rgb BOLT_COLOR notitle, \
	const10,t ls 3 lw 8 dt 2 lc rgb BOLT_COLOR notitle