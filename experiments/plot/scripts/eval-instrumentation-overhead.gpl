working_directory = system("dirname $(readlink -f ".ARG0.")")

set terminal postscript eps color solid enhanced font ',22' size 6.2,2.5
set encoding utf8
set output working_directory."/../figures/eval-instrumentation-overhead.eps"

set key bottom inside vertical left Left reverse font "Helvetica,22" width -4
set key at screen 0.19,screen 0.57 center center maxrows 3 width -1
set grid 
set key samplen 0.5

set boxwidth 0.9

set label 1 'Instrumentation rate (%)' font "Helvetica,22" at screen 0.39,0.05
set ylabel "Throughput (Mpps)" font "Helvetica,22" offset 1.5,0
unset xlabel
set yrange [2:]

set xtics ("1" 1, "5" 5,"25" 25,"50" 50,"75" 75,"100" 100) rotate by 45 right font ',20' offset 0,0.1
set ytics 1 nomirror
set logscale x 10
set datafile separator ","

#####################################
## Define colors used in the graph ##
#####################################
O0_COLOR = "#542437"                     #006400
O1_COLOR = "#53777a"                     #FFFF00
O2_COLOR = "#ffff00"                     #FFA500
O3_COLOR = "#ff6361"                     #FF0000
BOLT_COLOR = "#fa6900"                   #FF5145
AUTOFDO_COLOR = "#a7dbd8"                #FF9541
BOLT_AUTOFDO_COLOR = "#83af9b"           #FF4611
CACHED_COMP_COLOR = "#009900"
RUNTIME_CONF_COLOR = "#cc0000"    #ec7176"
MAT_SPECIALIZATION_COLOR = "#0000ff"       #f4ab33"     #FD1741
BLACK = "black"
OPTIMIZATION_COLOR = "#ff0000"

set multiplot layout 1,2 margins 0.07, 0.95, 0.20, 0.9 spacing 0.03

set title "Router" offset 0,-0.75

plot working_directory.'/../data/eval-instrumentation-overhead/router.dat'	using 1:2 axes x1y1 pt 9 lc rgb O3_COLOR ps 2.5 ti "Baseline", \
	 ''												                        using 1:2 axes x1y1 with lines lc rgb O3_COLOR notitle, \
	 ''				                                                        using 1:3 axes x1y1 pt 7 lc rgb RUNTIME_CONF_COLOR ps 2.5 ti "+Instr.", \
	 ''												                        using 1:3 axes x1y1 with lines lc rgb RUNTIME_CONF_COLOR notitle, \
	 ''				                                                        using 1:4 axes x1y1 pt 5  lc rgb BOLT_AUTOFDO_COLOR ps 2.5 ti "+Instr.+Opts.", \
	 ''												                        using 1:4 axes x1y1 with lines lc rgb BOLT_AUTOFDO_COLOR notitle


unset ylabel
unset yrange
unset key
unset ytics
set title "BPF-iptables"
set grid x y2
set y2range [0.5:2]
set y2tics 0.5 offset -0.9,0 nomirror

plot working_directory.'/../data/eval-instrumentation-overhead/bpf-iptables.dat'	using 1:2 axes x1y2 pt 9 lc rgb O3_COLOR ps 2.5 ti "Baseline", \
	 ''												                                using 1:2 axes x1y2 with lines lc rgb O3_COLOR notitle, \
	 ''				                                                                using 1:3 axes x1y2 pt 7 lc rgb RUNTIME_CONF_COLOR ps 2.5 ti "+Instr.", \
	 ''						                                						using 1:3 axes x1y2 with lines lc rgb RUNTIME_CONF_COLOR notitle, \
	 ''		                                                                		using 1:4 axes x1y2 pt 5  lc rgb BOLT_AUTOFDO_COLOR ps 2.5 ti "+Instr.+Opts.", \
	 ''							                                					using 1:4 axes x1y2 with lines lc rgb BOLT_AUTOFDO_COLOR notitle
