working_directory = system("dirname $(readlink -f ".ARG0.")")

set terminal postscript eps color solid enhanced font ',22' size 6,2.8
set encoding utf8
set output working_directory."/../figures/throughput-loc.eps"

set key top outside vertical right Left reverse font "Helvetica,22" maxrows 1 opaque
set key at screen 0.69,screen 0.85 center center maxrows 1 font "Helvetica,24"
set key samplen 2.5
set grid 

set style data histogram
set style histogram rowstack #gap 1
set style fill pattern border -1 #solid border -1
set boxwidth 0.9

unset xtics
set xtics ("Switch" 0, "Router" 1, "Katran" 2, "BPF-iptables" 3) rotate by 30 right font ',16' offset 0,0.3
set ylabel "Avg Throughput (Mpps)" font "Helvetica,22" offset 0,0
set xlabel offset 0,1
set ylabel offset 1.5,0
set yrange [0:6]

set datafile separator ","

#####################################
## Define colors used in the graph ##
#####################################
O0_COLOR = "#542437"                     #006400
O1_COLOR = "#53777a"                     #FFFF00
O2_COLOR = "#ffff00"                     #FFA500
O3_COLOR = "#ff6361"                     #FF0000
BOLT_COLOR = "#fa6900"                   #FF5145
AUTOFDO_COLOR = "#a7dbd8"                #FF9541
BOLT_AUTOFDO_COLOR = "#83af9b"           #FF4611
CACHED_COMP_COLOR = "#009900"
RUNTIME_CONF_COLOR = "#cc0000"    #ec7176"
MAT_SPECIALIZATION_COLOR = "#0000ff"       #f4ab33"     #FD1741
BLACK = "black"
OPTIMIZATION_COLOR = "#ff0000"

# left, right, bottom, top
set multiplot layout 1,3 margins 0.06, 0.99, 0.20, 0.9 spacing 0.0 

set title "High Locality" offset 0,-0.75
plot    working_directory.'/../data/eval-throughput/high_loc.dat'	using 2 ti "Baseline" linecolor rgb "gray80" fs pattern 6, \
	''					using ($3-$2) ti "Morpheus" linecolor rgb RUNTIME_CONF_COLOR fs pattern 9

unset ylabel
set ytics format ""

set title "Low Locality"
plot    working_directory.'/../data/eval-throughput/low_loc.dat'   	using 2 notitle linecolor rgb "gray80" fs pattern 6, \
        ''              			using ($3-$2) notitle linecolor rgb RUNTIME_CONF_COLOR fs pattern 9

set title "No Locality"
plot    working_directory.'/../data/eval-throughput/no_loc.dat'   	using 2 notitle linecolor rgb "gray80" fs pattern 6, \
        ''              			using ($3-$2) notitle linecolor rgb RUNTIME_CONF_COLOR fs pattern 9